import org.apache.tools.ant.filters.*

buildscript {
    ext{
        versionPlugin = '1' // Increment this after every successful build on http://ci.onarandombox.com
        versionKotlinMajor = '1.1'
        versionKotlin = "$versionKotlinMajor.3"
        versionBukkit = '1.12-R0.1-SNAPSHOT'
    }

    repositories {
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$versionKotlin"
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:0.9.15"
    }
}

plugins {
    id "com.github.johnrengelman.shadow" version "2.0.1"
    id 'net.minecrell.licenser' version '0.3'
}

apply plugin: 'kotlin'
apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: 'maven'
apply plugin: 'org.jetbrains.dokka'
apply plugin: 'net.minecrell.licenser'

install.dependsOn shadowJar
uploadArchives.dependsOn shadowJar
assemble.dependsOn shadowJar

sourceCompatibility = 1.6
targetCompatibility = 1.6

repositories {
    mavenCentral()
    maven {
        url "https://hub.spigotmc.org/nexus/content/repositories/snapshots/"
    }
    maven {
        url "http://repo.bstats.org/content/repositories/releases/"
    }
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-jre7:$versionKotlin"
    compile "org.bstats:bstats-bukkit:1.1"
    compileOnly "org.bukkit:bukkit:$versionBukkit"
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.6"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.6"
}

shadowJar {
    from "${projectDir}/LICENSE.txt"
    from ("${projectDir}/KOTLIN-LICENSE.txt") {
        into ("kotlin")
    }
    baseName = project.name
    classifier = null
    dependencies {
        exclude(dependency("org.jetbrains:annotations:.*"))
    }
    relocate 'org.bstats', 'bstats.libkt'
}

dokka {
    outputFormat = 'javadoc'
    outputDirectory = "$buildDir/javadoc"
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier 'sources'
    from sourceSets.main.allSource
    from "${projectDir}/LICENSE.txt"
}

task javadocJar(type: Jar) {
    classifier 'javadoc'
    from dokka
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

group 'com.github.libkt'
version "$versionPlugin"

processResources {
    filter ReplaceTokens, tokens: [
            "versionPlugin": project.property("versionPlugin"),
            "versionKotlinMajor": project.property("versionKotlinMajor"),
            "versionKotlin": project.property("versionKotlin").toString()
    ]
}

license {
    header project.file('HEADER.txt')
    include '**/*.kt'
    newLine false
}

if (project.hasProperty('mavenUser') && project.hasProperty('mavenPassword')) {
    shadowJar.dependsOn checkLicenses
    uploadArchives {
        repositories {
            mavenDeployer {
                repository(url: "http://repo.onarandombox.com/content/repositories/thirdparty/") {
                    authentication(userName: mavenUser, password: mavenPassword)
                }
                snapshotRepository(url: "http://repo.onarandombox.com/content/repositories/thirdparty-dev/") {
                    authentication(userName: mavenUser, password: mavenPassword)
                }
            }
        }
    }
}
