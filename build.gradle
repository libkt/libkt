import org.apache.tools.ant.filters.*

buildscript {
    ext{
        buildNumber = '1' // Increment this after every successful build on http://ci.onarandombox.com
        versionKotlinMajor = '1.1'
        versionKotlin = "$versionKotlinMajor.4"
        versionPlugin = "$buildNumber-$versionKotlinMajor"
    }

    repositories {
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$versionKotlin"
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:0.9.15"
    }
}

plugins {
    id "com.github.johnrengelman.shadow" version "2.0.1"
    id 'net.minecrell.licenser' version '0.3'
}

apply plugin: 'kotlin'
apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: 'maven'
apply plugin: 'org.jetbrains.dokka'
apply plugin: 'net.minecrell.licenser'

jar.finalizedBy shadowJar

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenLocal()
    mavenCentral()
    jcenter()
    maven {
        url "https://dl.bintray.com/kotlin/exposed/"
    }
    maven {
        url "https://hub.spigotmc.org/nexus/content/repositories/snapshots/"
    }
    maven {
        url "http://repo.bstats.org/content/repositories/releases/"
    }
    maven {
        url "http://nexus.okkero.com/repository/maven-releases/"
    }
}

dependencies {
    compile ("org.jetbrains.kotlin:kotlin-stdlib-jre8:$versionKotlin") {
        exclude group: 'org.jetbrains', module: 'annotations'
    }
    compile "org.jetbrains.kotlin:kotlin-reflect"
    compile 'org.jetbrains.kotlinx:kotlinx-coroutines-core:0.17'

    compile 'io.github.microutils:kotlin-logging:1.4.6'
    compile 'org.slf4j:slf4j-api:1.7.21'
    compile 'ch.qos.logback:logback-classic:1.1.3'

    compile 'org.jetbrains.exposed:exposed:0.8.5'
    compile 'com.h2database:h2:1.4.191'

    compile "com.okkero.skedule:skedule:1.2.2"
    compile "org.bstats:bstats-bukkit:1.1"

    compileOnly "org.bukkit:bukkit:1.12.1-R0.1-SNAPSHOT"
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

shadowJar {
    baseName = project.name
    classifier = null
    relocate 'org.bstats', 'bstats.libkt'
}

dokka {
    outputFormat = 'javadoc'
    outputDirectory = "$buildDir/javadoc"
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier 'sources'
    from sourceSets.main.allSource
    from "${projectDir}/LICENSE.txt"
}

task javadocJar(type: Jar) {
    classifier 'javadoc'
    from dokka
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

group 'com.github.libkt'
version "$versionPlugin"

processResources {
    filter ReplaceTokens, tokens: [
            "buildNumber": project.property("buildNumber").toString(),
            "versionKotlinMajor": project.property("versionKotlinMajor"),
            "versionKotlin": project.property("versionKotlin").toString()
    ]
}

license {
    header project.file('HEADER.txt')
    include '**/*.kt'
    newLine false
}

def installer = install.repositories.mavenInstaller
if (project.hasProperty('mavenUser') && project.hasProperty('mavenPassword')) {
    shadowJar.dependsOn checkLicenses
    uploadArchives {
        repositories {
            mavenDeployer {
                repository(url: "http://repo.onarandombox.com/content/repositories/thirdparty/") {
                    authentication(userName: mavenUser, password: mavenPassword)
                }
                snapshotRepository(url: "http://repo.onarandombox.com/content/repositories/thirdparty-dev/") {
                    authentication(userName: mavenUser, password: mavenPassword)
                }
            }
        }
    }

    def deployer = uploadArchives.repositories.mavenDeployer
    [installer, deployer]*.pom*.whenConfigured {pom ->
        pom.dependencies.removeAll { it.groupId == 'org.bstats' }
    }
} else {
    [installer]*.pom*.whenConfigured {pom ->
        pom.dependencies.removeAll { it.groupId == 'org.bstats' }
    }
}

