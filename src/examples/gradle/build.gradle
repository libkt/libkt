plugins {
    id "com.github.johnrengelman.shadow" version "2.0.1"
    // Version of Kotlin should be compatible with libkt version
    id "org.jetbrains.kotlin.jvm" version "1.1.3-2"
}

apply plugin: 'kotlin'
apply plugin: 'com.github.johnrengelman.shadow'

ext {
    java_version = '1.8'
    libkt_version = '1.1-b1'
    bukkit_version = '1.12-R0.1-SNAPSHOT'
}

// Update these
group 'your.group.id'
version '1.0-SNAPSHOT'

jar.finalizedBy shadowJar

repositories {
    mavenCentral()
    maven {
        url "https://hub.spigotmc.org/nexus/content/repositories/snapshots/"
    }
    maven {
        // This is the repo that contains libkt
        url "http://repo.onarandombox.com/content/groups/public/"
    }
}

dependencies {
    compileOnly "com.github.libkt:libkt:$libkt_version"
    compileOnly "org.bukkit:bukkit:$bukkit_version"
}

// This is required because libkt relocates the Kotlin packages and the compiler doesn't know that
// Be aware that any compile scope dependencies will be automatically included in your jar with this configuration
shadowJar {
    baseName = project.name
    classifier = null

    // This part is very important - do not change
    relocate 'kotlin', "koltin.v${libkt_version.split("-")[0].replace('.', '_')}"
}

compileKotlin {
    kotlinOptions.jvmTarget = "$java_version"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "$java_version"
}